#version 450 core

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct Particle {
  vec2 position;
  vec2 velocity;
  uint type;
};
layout (std430, binding = 0) buffer particlesSSBO {
  Particle particles[];
};

uniform float deltaTime;

layout (std140, binding = 1) uniform simulationUBO {
  vec4 groupsRules[16];
  vec4 groupsColors[8];
  uint particlesCount;
  uint groupsCount;
  float viscosity;
};

void main() {
  Particle self = particles[gl_GlobalInvocationID.x];
  vec2 force = vec2(0.0, 0.0);

  // uint particleCount = gl_NumWorkGroups.x * gl_WorkGroupSize.x;
  for (uint i = 0; i < particlesCount; i++) {
    if (i == gl_GlobalInvocationID.x) {
      continue;
    }

    Particle p = particles[i];

    vec2 diff = self.position - p.position;
    float mag = length(diff);
    if (mag == 0.0f || mag > 0.2f) {
      continue;
    }

    uint index = self.type * groupsCount + p.type;
    float rule = groupsRules[index / 4][index % 4];
    force += diff * 0.1f * rule / mag;
  }
  self.velocity *= 1.0f - viscosity;
  self.velocity += force * deltaTime;

  particles[gl_GlobalInvocationID.x] = self;
}
